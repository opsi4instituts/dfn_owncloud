; This sourcecode is partly owned by uib.de
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/
;
; common generated by  "opsi-setup-detector" 
; modified  by GEI.de  (Detlef Krummel  opsi@gei.de)
; modified  by WZB.eu  (Eric Esser      opsi@wzb.eu)

[Actions]
requiredWinstVersion >= "4.11.4.4"
include_append "%ScriptDrive%\lib\check_nsis-exitcode.ins"

DefVar $LogDir$ 
DefVar $LogLevel$
DefVar $ProductId$ 
DefVar $MinimumSpace$
Defvar $SetupType$
DefVar $SetupFile$
DefVar $InstallDir$
DefVar $InstallDirExe$
DefVar $ExitCode$
DefVar $ErrorMsg$
DefVar $LicenseRequired$
DefVar $LicenseKey$
DefVar $LicensePool$
DefVar $SilentSwitch$
DefVar $UninstallProgram$ 
DefVar $INST_SystemType$
DefVar $UpdateNotifiy$
DefVar $PrettyName$

Set $PrettyName$ = "OwnCloud"
Set $LogDir$	= "%opsiLogDir%"
SetLogLevel 	= 6

Set $SetupType$		= "nsis"
Set $ProductId$		= "owncloud"
Set $MinimumSpace$    	= "175 MB"				
; von Nullsoft abweichender Pfad wegen einfacher DeInstallation
Set $InstallDir$      	= "%ProgramFiles32Dir%\" + $ProductId$
Set $InstallDirExe$	= $InstallDir$ + "\" + $ProductId$ + ".exe"
Set $SilentSwitch$	= "/D=" + $InstallDir$

DefVar	$PackageVersion$
DefVar	$SetupVersion$
Set $PackageVersion$	= takeString(1, splitString ("%installingProdVersion%","-"))
Set $SetupVersion$	= strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))
Set $SetupFile$ 	= "ownCloud-" + $SetupVersion$ + "-setup.exe"	

Set $LicenseRequired$ = "false"
Set $LicensePool$     = "p_" + $ProductId$

; read property from OPSI/control
DefVar 	$control_AllUserDesktopLink$
Set 	$control_AllUserDesktopLink$ = GetProductProperty("AllUserDesktopLink","True")
; Eric Esser
Set 	$UpdateNotifiy$   = GetProductProperty("update_notify", "false")
Set 	$INST_SystemType$ = GetSystemType
; ----------------------------------------------------------------

ChangeDirectory "%SCRIPTPATH%"
if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
        LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
        isFatalError "No Space"
else
        comment "Space OK"
endif
if not( FileExists($Setupfile$) )
        LogError "Setupfile NOT exists (" + $Setupfile$ +")"
        isFatalError "No Setupfile found"
else
        comment "Setupfile exists ..."
endif 

ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " +$SetupVersion$	
if FileExists("%ScriptPath%\delsub32.opsiscript")
	comment "Start uninstall sub section"
	Sub "%ScriptPath%\delsub32.opsiscript"
endif

Message "Installiere " + $ProductId$ + " (%InstallingProdVersion%)..."
comment "Start setup program"
killtask "owncloud.exe"
Winbatch_install /WaitForProcessEnding "owncloud.exe" /TimeOutSeconds 20
;Sub_check_exitcode
if $control_AllUserDesktopLink$ = "True"
	comment "Create shortcuts"
	LinkFolder_install
endif

;Set registry keys
if ($INST_SystemType$ = "x86 System")
	if ($UpdateNotifiy$ = "True")
		Registry_setUpdateNotifyOn32
	else
		Registry_setUpdateNotifyOff32
	endif
endif
if ($INST_SystemType$ = "64 Bit System")
	if ($UpdateNotifiy$ = "True")
		Registry_setUpdateNotifyOn64
	else
		Registry_setUpdateNotifyOff64
	endif
endif

; ----------------------------------------------------------------
; install sections
; ----------------------------------------------------------------
[Winbatch_install]
"%ScriptPath%\$SetupFile$" /S $SilentSwitch$

[Registry_setUpdateNotifyOn32]
openkey [HKEY_LOCAL_MACHINE\Software\ownCloud\ownCloud]
Set "skipUpdateCheck" = REG_DWORD:0000

[Registry_setUpdateNotifyOff32]
openkey [HKEY_LOCAL_MACHINE\Software\ownCloud\ownCloud]
Set "skipUpdateCheck" = REG_DWORD:0001

[Registry_setUpdateNotifyOn64]
openkey [HKEY_LOCAL_MACHINE\Software\Wow6432Node\ownCloud\ownCloud]
Set "skipUpdateCheck" = REG_DWORD:0000

[Registry_setUpdateNotifyOff64]
openkey [HKEY_LOCAL_MACHINE\Software\Wow6432Node\ownCloud\ownCloud]
Set "skipUpdateCheck" = REG_DWORD:0001

[Winbatch_install]
"%ScriptPath%\$SetupFile$" /S $SilentSwitch$

[LinkFolder_install]
; Example of creating an shortcut to the installed exe on AllUsers desktop:
;
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
 	name: $ProductId$
 	target: $InstallDirExe$
 	parameters: 
 	working_dir: $InstallDir$
 	icon_file: 
 	icon_index: 
end_link
